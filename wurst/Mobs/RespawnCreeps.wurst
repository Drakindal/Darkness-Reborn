package RespawnCreeps
import MyUnitIds
import ClosureTimers
import Global_Variables
import ItemDrops
import ClosureEvents
import HashMap
import TowerAbilities
import ClosureForGroups
let creepRespawns = new HashMap<int,real>
let currentCreep = new HashMap<int,int>
let currentRespawn = new HashMap<int,CallbackSingle>
player hostile = players[PLAYER_NEUTRAL_AGGRESSIVE]
unit array leftCreep = []
unit array eliteCreep = []
unit array bossCreep = []
unit array overlord = []
unit array dryad = []
real facing = 270
real facingUp = 90
integer frostyCounter = 0
init
    creepRespawner()

public function player.setCreepSettings(int unitId, vec2 pos)
    let pid = this.getId()
    let currentU = currentCreep.get(pid)
    if currentU != unitId
        forUnitsInRange(pos, 600) (unit u) ->
            if u.isEnemyOf(this)
                u.remove()
        if currentRespawn.get(pid) != null
            destroy currentRespawn.getAndRemove(pid)
        createUnit(hostile, unitId, pos)
        currentCreep.put(pid, unitId)

function creepRespawner()
    EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
        let u = GetDyingUnit()
        let killOwnerId = GetKillingUnit().getOwner().getId()
        let pos = u.getPos()
        let rot = u.getFacingAngle()
        let uid = u.getTypeId()
        
        u.dropItems()         
        if currentCreep.get(killOwnerId) == uid
            let doAf = doAfter(creepRespawns.get(uid)) ->
                if(currentCreep.get(killOwnerId) == uid)
                    createUnit(hostile, uid, pos, rot)
                    currentCreep.put(killOwnerId, uid)
            currentRespawn.put(killOwnerId, doAf)
        else
            if u.isIllusion()
                skip
            else
                if creepRespawns.has(uid)
                    if uid == ELITE_CREEP[9]
                        frostyCounter++
                    doAfter(creepRespawns.get(uid)) ->
                        createUnit(hostile, uid, pos, rot)
        
public function eliteSideCreepsSetup()
    for int i = 1 to 24
        creepRespawns.put(LEFT_CREEP[i], 1)

    for i = 1 to 5
        creepRespawns.put(ELITE_CREEP[i], 5)
        createUnit(hostile, ELITE_CREEP[i], eliteLoc[i].getCenter(), facingUp.asAngleDegrees())
        
    for i = 6 to 10
        if i == 6 or i == 7
            creepRespawns.put(ELITE_CREEP[i], 1)
        if i == 8 or i == 10
            creepRespawns.put(ELITE_CREEP[i], 10)
        if i == 9
            creepRespawns.put(ELITE_CREEP[i], 30)
                
        createUnit(hostile, ELITE_CREEP[i], eliteLoc[i].getCenter(), facing.asAngleDegrees())

function dragonCreep(unit u)
    EventListener.add(u, EVENT_PLAYER_UNIT_DEATH) -> 
        doAfter(30) ->
            for i = 1 to 7
                if u == bossCreep[i]
                    bossCreep[i+1] = createUnit(hostile, (BOSS_CREEP[i+1]), bossLoc[1].getCenter(), facing.asAngleDegrees())
                    dragonCreep(bossCreep[i+1])
        u.dropItems()
                    
function seaCreep(unit u)
    EventListener.add(u, EVENT_PLAYER_UNIT_DEATH) -> 
        doAfter(30) ->
            for i = 9 to 11
                if u == bossCreep[i]
                    bossCreep[i+1] = createUnit(hostile, (BOSS_CREEP[i+1]), bossLoc[2].getCenter(), facing.asAngleDegrees())
                    seaCreep(bossCreep[i+1])
        u.dropItems()

function dreadlordCreep(unit u)
    EventListener.add(u, EVENT_PLAYER_UNIT_DEATH) -> 
        doAfter(30) ->
            if u == bossCreep[13]
                bossCreep[14] = createUnit(hostile, (BOSS_CREEP[14]), bossLoc[4].getCenter(), facing.asAngleDegrees())
                bossCreep[14].setLevel(80, false)
                bossCreep[14].suspendXp(true)
                dreadlordCreep(bossCreep[14])
        u.dropItems()

function towerCreep(unit u)
    if(u == bossCreep[15])
        u.startTidal()
    else if(u == bossCreep[16])
        u.startTreant1()
    else if(u == bossCreep[17])
        u.startTreant2()
    else if(u == bossCreep[18])
        u.startTreant3()
            
    EventListener.add(u, EVENT_PLAYER_UNIT_DEATH) -> 
        doAfter(30) ->
            for i = 15 to 21
                if u == bossCreep[i]
                    bossCreep[i+1] = createUnit(hostile, (BOSS_CREEP[i+1]), bossLoc[3].getCenter(), facing.asAngleDegrees())
                    towerCreep(bossCreep[i+1])
        u.dropItems()

public function bossCreepSetup()
    bossCreep[1] = createUnit(hostile, BOSS_CREEP[1], bossLoc[1].getCenter(), facing.asAngleDegrees()) // Dragon
    dragonCreep(bossCreep[1])
    bossCreep[9] = createUnit(hostile, BOSS_CREEP[9], bossLoc[2].getCenter(), facing.asAngleDegrees()) // Sea
    seaCreep(bossCreep[9])
    bossCreep[13] = createUnit(hostile, BOSS_CREEP[13], bossLoc[4].getCenter(), facing.asAngleDegrees()) // Dreadlord
    bossCreep[13].setLevel(80, false)
    bossCreep[13].suspendXp(true)
    dreadlordCreep(bossCreep[13])
    bossCreep[15] = createUnit(hostile, BOSS_CREEP[15], bossLoc[3].getCenter(), facing.asAngleDegrees()) // Tower
    towerCreep(bossCreep[15])
    
function overdryaCreep(unit u)
    EventListener.add(u, EVENT_PLAYER_UNIT_DEATH) ->
        let u1 = GetDyingUnit()
        let pos = u1.getPos()
        doAfter(5) -> 
            for i = 1 to 10
                if u1 == overlord[i]
                    overlord[i] = createUnit(hostile, OVERDRYA_CREEP[2], pos, u1.getFacingAngle())
                    overdryaCreep(overlord[i])
                else if u1 == dryad[i]
                    dryad[i] = createUnit(hostile, OVERDRYA_CREEP[1], pos, u1.getFacingAngle())
                    overdryaCreep(dryad[i])

public function overdryaSetup()
    for i = 1 to 10
        overlord[i] = createUnit(hostile, OVERDRYA_CREEP[2], gg_rct_OverlordArea.randomPoint(), facing.asAngleDegrees())
        overdryaCreep(overlord[i])
        dryad[i] = createUnit(hostile, OVERDRYA_CREEP[1], gg_rct_DryadArea.randomPoint(), facing.asAngleDegrees())
        overdryaCreep(dryad[i])