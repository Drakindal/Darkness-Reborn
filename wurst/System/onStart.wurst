package onStart
import ClosureTimers
import WaveSetup
import DialogBox
import MyBuildingsIds
import RespawnCreeps
import MyUnitIds
import HeroRespawnTimer
import CodeDialog
import Global_Variables
import TeleportToMobs
import GameEnd
public unit elementalSoul
real facing = 270
real facingUp = 90

init

    mapSetup()

    doAfter(0.01) -> 
        for i = 0 to 9
            gg_cam_CameraHeroPick.apply(true, players[i], 0.3)
            CreateFogModifierRectBJ( true, players[i], FOG_OF_WAR_VISIBLE, GetPlayableMapRect() )

public function heroRespawn(unit u)
    CreateTrigger()..registerUnitEvent(u, EVENT_UNIT_DEATH)..addAction() ->
        heroRespawnTimer(GetDyingUnit())
        let i = GetOwningPlayer(GetTriggerUnit()).getId()
        if GetOwningPlayer(GetDyingUnit()) == Player(i)
            doAfter(10) -> 
                playerHero[i].revive(gg_rct_Base.getCenter(), true)

public function questSetup()
    let commands = CreateQuest()
    let discord = CreateQuest()
    let recipe = CreateQuest()
    
    QuestSetDiscovered(commands, true)
    QuestSetIconPath(commands, "ReplaceableTextures\\CommandButtons\\BTNSpy.blp")
    QuestSetTitle(commands, "Commands")
    QuestSetDescription(commands, "-cam 500-4000 : Adjusts the camera view \n-clean : Cleans items in 10 seconds\n-stopclean : Stops cleaning process")

    QuestSetDiscovered(discord, true)
    QuestSetIconPath(discord, "ReplaceableTextures\\Icons\\discordIcon.blp")
    QuestSetTitle(discord, "Discord")
    QuestSetDescription(discord, "Discord invite: https://discord.gg/pnrhrHS")

    QuestSetDiscovered(recipe, true)
    QuestSetRequired(recipe, false)
    QuestSetIconPath(recipe, "ReplaceableTextures\\CommandButtons\\BTNAdvStruct.blp")
    QuestSetTitle(recipe, "Recipe for non-store items")
    QuestSetDescription(recipe, "Khadgar's Pendant of Health + Periapt of Vitality = Jewelry of Life\n"
    +"Pendant of Mana + Pendant of Energy = jewelry of Mana\n"
    +"Jewelry of Life + Periapt of Vitality + Ring of Regeneration = Guinevere's Ring\n"
    +"Jewelry of Mana + Pendant of Mana = Lancelot's Ring\n"
    +"Lancelot's Ring + Guinevere's Ring + Pendant of Energy = Arkenstone Fragment")

function onBuyHero(unit u)
    CreateTrigger()..registerUnitEvent(u, EVENT_UNIT_SELL)..addAction() ->
        let soldUnit = GetSoldUnit()
        for i = 0 to 9
            if GetOwningPlayer(soldUnit) == players[i]
                playerHero[i] = soldUnit
                gg_cam_CameraHeroPicked.apply(true, players[i], 0.5)
                heroRespawn(playerHero[i])
                playerHero[i].setPos(gg_rct_Base.getCenter())
                players[i].setState(PLAYER_STATE_RESOURCE_FOOD_CAP, 0)
                GetBuyingUnit().remove()

function createSetup()
    let p = Player(10)

    SetPlayerState(Player(11), PLAYER_STATE_GIVES_BOUNTY, 1)
    SetPlayerState(Player(12), PLAYER_STATE_GIVES_BOUNTY, 1)
    SetPlayerState(Player(13), PLAYER_STATE_GIVES_BOUNTY, 1)

    for i = 0 to 9
        createUnit(players[i], HERO_PICKER, gg_rct_HeroPicker.getCenter())
        players[i]..addState(PLAYER_STATE_RESOURCE_FOOD_CAP, 5)..addState(PLAYER_STATE_RESOURCE_GOLD, 800)
        


    let tavern1 = CreateUnit(Player(10), BUILDING_TAVERN_STR, -9216.0, -12480, bj_UNIT_FACING)
    onBuyHero(tavern1)
    let tavern2 = CreateUnit(Player(10), BUILDING_TAVERN_AGI, -8832.0, -12480, bj_UNIT_FACING)
    onBuyHero(tavern2)
    let tavern3 = CreateUnit(Player(10), BUILDING_TAVERN_INT, -8448.0, -12480, bj_UNIT_FACING)
    onBuyHero(tavern3)

    CreateUnit(p, BUILDING_4, - 1479.0, - 5505.8, 270.000) // Basic Items 4
    CreateUnit(p, BUILDING_3, - 1483.8, - 5289.4, 270.000) // Basic Items 3
    CreateUnit(p, BUILDING_2, - 1479.6, - 5061.5, 270.000) // Basic Items 2
    CreateUnit(p, BUILDING_1, - 1478.7, - 4841.5, 270.000) // Basic Items 1
    let endGamebosses = CreateUnit(p, BUILDING_17, 204.8, - 5520.4, 270.000) // End-game bosses
    let eliteGrounds = CreateUnit(p, BUILDING_16, 202.2, - 5313.4, 270.000) // Elite hunting grounds
    let intermediateGrounds = CreateUnit(p, BUILDING_15, 195.2, - 5066.8, 270.000) // Intermediate hunting grounds
    let easyGrounds = CreateUnit(p, BUILDING_14, 190.9, - 4837.0, 270.000) // Easy hunting grounds
    CreateUnit(p, BUILDING_8, - 1740.5, - 5484.2, 270.000) // Recipe shop 4
    CreateUnit(p, BUILDING_7, - 1734.9, - 5263.3, 270.000) // Recipe Shop 3
    CreateUnit(p, BUILDING_6, - 1730.9, - 5047.2, 270.000) // Recipe Shop 2
    CreateUnit(p, BUILDING_5, - 1734.1, - 4827.9, 270.000) // Recipe Shop 1
    CreateUnit(p, BUILDING_13, 468.8, - 5517.1, 270.000) // End Game Items
    CreateUnit(p, BUILDING_12, 466.5, - 5291.8, 270.000) // Decent Items
    CreateUnit(p, BUILDING_11, 455.5, - 5074.8, 270.000) // Late Game Items
    CreateUnit(p, BUILDING_10, 459.8, - 4848.5, 270.000) // Late Game Items

    teleportToMobs(easyGrounds, intermediateGrounds, eliteGrounds, endGamebosses)

function mapSetup()

    elementalSoul = createUnit(Player(10), ELEMENTAL_SOUL, gg_rct_AllSpawnMove2.getCenter(), facing.asAngleDegrees()) // Elemental Soul
    onDefeat(elementalSoul)
    
    CreateUnit(Player(10), BUILDING_0, - 640.0, - 5248.0, 270.000) // Fountain of Healing (at base)


    doAfter(0.5) -> 

        let dBox = new DialogBox("Choose difficulty")

        dBox.addButton("Normal") ->
            difficulty = "normal"
            preWaveSetup()
            questSetup()
            createSetup()
            leftSideCreepsSetup()
            eliteSideCreepsSetup()
            bossCreepSetup()
            overdryaSetup()
            codeDialog()
            destroy dBox
        
        dBox.display(players[0], true)