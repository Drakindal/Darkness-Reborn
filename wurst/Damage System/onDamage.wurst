package onDamage
import MyUnitIds
// import DamageDetection
import DamageEvent
import MyAbilityIds
import Platinum1Stun
import Platinum2Stun
import LancelotSet
import HelmofHermes
import LancelotShield
import HolyGoldShield
import DeadEyes
import NobleSword
import NerubianStaff
import RavenBlade
import ApolloHelm
import CrystalArmor
import CrystalSpear
import BerserkerStaff
import Frostmourne
import ThunderBow
import ThunderBow2
import OrbofLightning
import codeDamageTypings
import MyItemIds
import MyBuffIds
import WaveHeroSetup
import PerfectAquamrina
import ReactiveArmor1
import ReactiveArmor2
import SpikedArmor
import SplitArmor
import DragonTurtle
import Turtle
import A0RI_R
import A0RL_E
import ThornsAuraWave
import SoulKilJaeden
import SpikeOfCrypt
import Global_Variables

@configurable constant DETECT_NATIVE_ABILITIES = true


let difficultyDamageIncrease = [0.5, 0.75, 1., 1.5, 2.]

texttag justaposeCrit

function unit.calculateMagicRes() returns real
    let magicResCalc = this.getRes(resistanceType.MAGICAL)
    return 1-(magicResCalc)

function returnReducedDamage() returns real
    return DamageEvent.getUnreducedOriginalAmount() - DamageEvent.getAmount()

init
    DamageEvent.addListener(0) -> 
        let src = DamageEvent.getSource()
        let targ = DamageEvent.getTarget()
        src.wasAttacked()
        if(src.isEnemyOf(players[10]))
            DamageEvent.setAmount(DamageEvent.getAmount() * difficultyDamageIncrease[gameMode])
        switch DamageEvent.getType()
            case ATTACK
                if(returnReducedDamage() > 0)
                    DamageEvent.addAmount(returnReducedDamage() * src.getPen(penetrationType.PHYSICAL))
                if src.isType(UNIT_TYPE_RANGED_ATTACKER)
                    physicalDamageChecks(0, src, targ, WEAPON_TYPE_AXE_MEDIUM_CHOP)
                else
                    physicalDamageChecks(0, src, targ, WEAPON_TYPE_WHOKNOWS)

                if src.hasAbility('B003')
                    src.removeAbility('B003')

            case SPELL
                magicDamageChecks(0, src, targ)
                
            case CODE
                let damId = DamageEvent.getId()
                DamageEvent.setAmount(DamageEvent.getAmount() * 1.111111111)
                if(DamageEvent.getDamageType() == DAMAGE_TYPE_NORMAL)
                    if(returnReducedDamage() > 0 and unitStatMap.has(src.getTypeId()))
                        DamageEvent.addAmount(returnReducedDamage() * src.getPen(penetrationType.PHYSICAL))
                    physicalDamageChecks(damId, src, targ, DamageEvent.getWeaponType())
                else if (DamageEvent.getDamageType() == DAMAGE_TYPE_MAGIC)
                    magicDamageChecks(damId, src, targ)
            default

    DamageEvent.addListener(1) ->
        let src = DamageEvent.getSource()
        let targ = DamageEvent.getTarget()
        if (DamageEvent.getType() == ATTACK)
            let damtyp = DamageEvent.getDamageType()
            if (damtyp == DAMAGE_TYPE_NORMAL or damtyp == DAMAGE_TYPE_MAGIC)
                //Reflection for Melee only
                if(src.isType(UNIT_TYPE_MELEE_ATTACKER))
                    reflectionChecks(src, targ)
                //HermesHelm
                if targ.hasItemById(ITEM_HELM_HERMES)
                    hermesHelm(targ, src)
                //Lancelot Armor
                if targ.hasItemById(ITEM_LANCELOT_ARMOR) and GetRandomInt(0, 100) < 20
                    lancelotSetMana(targ)
                //LanceSet
                if targ.hasItemById(ITEM_LANCELOT_SET)
                    if(GetRandomInt(0, 100) < 30)
                        lancelotSetMana(targ)
                    if(GetRandomInt(0, 100) < 50)
                        lancelotSetHealth(targ)
                //LanceShield
                if targ.hasItemById(ITEM_LANCELOT_SHIELD) and GetRandomInt(0, 100) < 30
                    lancelotShield(targ)
                //holyGoldShield
                if targ.hasItemById(ITEM_HOLY_GOLD_SHIELD) and GetRandomInt(0, 100) < 50
                    holyGoldShield(targ)
                //ApolloHelm
                if targ.hasItemById(ITEM_APOLLOS_HELM) and GetRandomInt(0, 100) < 33 
                    apolloHelm(targ, src)
                //Crystal Armor
                if targ.hasItemById(ITEM_CRYSTAL_ARMOR) and GetRandomInt(0, 100) < 50
                    crystalArmor(targ, src)
                //SoulOfKiljaeden
                if src.hasItemById(ITEM_ORB_OF_KILJAEDAN) and GetRandomInt(0, 100) <= 47
                    kiljaedenSoul(src)

    DamageEvent.addListener(2) ->
        let src = DamageEvent.getSource()
        let targ = DamageEvent.getTarget()
        if (DamageEvent.getType() == ATTACK)
            if src.isIllusion()
                skip
            else
                let damtyp = DamageEvent.getDamageType()
                if (damtyp == DAMAGE_TYPE_NORMAL or damtyp == DAMAGE_TYPE_MAGIC)
                    //deadEyes
                    if src.hasItemById(ITEM_DEAD_EYES) and GetRandomInt(0, 100) < 14
                        deadEyes(src, targ)

                    //Noble Sword levels, integer is additive with 2xagi
                    if src.hasItemById(ITEM_NOBLE_SWORD_L1) and GetRandomInt(0, 100) < 11
                        nobleSword(src, targ, 0)

                    if src.hasItemById(ITEM_NOBLE_SWORD_L2) and GetRandomInt(0, 100) < 12
                        nobleSword(src, targ, 1)

                    if src.hasItemById(ITEM_NOBLE_SWORD_L3) and GetRandomInt(0, 100) < 13
                        nobleSword(src, targ, 2)

                    if src.hasItemById(ITEM_NOBLE_SWORD_L4) and GetRandomInt(0, 100) < 14
                        nobleSword(src, targ, 3)

                    if src.hasItemById(ITEM_BERSERKER_SWORD) and GetRandomInt(0, 100) < 18
                        nobleSword(src, targ, 4)

                    //Raven Blade levels, integer is multiplier by 2xstr
                    if src.hasItemById(ITEM_RAVEN_BLADE_L4) and GetRandomInt(0, 100) < 12
                        ravenBlade(src, targ, 1)

                    if src.hasItemById(ITEM_RAVEN_BLADE_L5) and GetRandomInt(0, 100) < 13
                        ravenBlade(src, targ, 2)

                    if src.hasItemById(ITEM_BERSERKERS_BLADE) and GetRandomInt(0, 100) < 14
                        ravenBlade(src, targ, 3)
                    //Crystal Spear
                    if src.hasItemById(ITEM_CRYSTAL_SPEAR) and GetRandomInt(0, 100) < 15
                        crystalSpear(src, targ)
                    //Int Staff
                    if src.hasItemById(ITEM_BERSERKERS_STAFF) and GetRandomInt(0, 100) < 24
                        berserkerStaff(src, targ)

                    if src.hasItemById(ITEM_NERUBIAN_STAFF_L1) and GetRandomInt(0, 100) < 24
                        nerubianStaff(src, targ)
                    //Frostmourne
                    if src.hasItemById(ITEM_FROSMOUNE_L5) and GetRandomInt(0, 100) < 15
                        frostmourne(targ)
                    //Orb of Lightning
                    if src.hasItemById(ITEM_ORB_OF_LIGHTNING) and GetRandomInt(0, 100) < 20
                        orbofLightning(src)
                    //ThunderBows
                    if src.hasItemById(ITEM_THUNDER_BOW) and GetRandomInt(0, 100) < 10
                        thunderBow(src)

                    if src.hasItemById(ITEM_THUNDER_BOW_II) and GetRandomInt(0, 100) < 15
                        thunderBow2(src)

                    //Platinum 1
                    if src.hasItemById(ITEM_PLATINUM_HAMMER) and GetRandomInt(0, 100) < 25
                        platinum1Stun(src, targ)
                    //Platinum 2
                    if src.hasItemById(ITEM_PLATINUM_HAMMER_II) and GetRandomInt(0, 100) < 25
                        platinum2Stun(src, targ)
    
function magicDamageChecks(int damId, unit src, unit targ)
    if(targ.getAbilityLevel(UTHER_ABIL_3) > 0)
        DamageEvent.setAmount(DamageEvent.getAmount() * (1-0.02*targ.getAbilityLevel(UTHER_ABIL_3)))
    DamageEvent.setAmount(DamageEvent.getAmount() * targ.calculateMagicRes())

function physicalDamageChecks(int damId, unit src, unit targ, weapontype notRanged)
    // Uther Damage reduction
    if(targ.getAbilityLevel(UTHER_ABIL_3) > 0)
        DamageEvent.setAmount(DamageEvent.getAmount() * (1-0.02*targ.getAbilityLevel(UTHER_ABIL_3)))
    // Wolfman attack
    if src.hasAbility(WOLF_BUFF_1)
        src.addHP(DamageEvent.getAmount() * 0.25)
    // Reflection non-ranged
    if(notRanged == WEAPON_TYPE_WHOKNOWS)
        reflectionChecks(src,targ)


function reflectionChecks(unit src, unit targ)
    let dam = DamageEvent.getAmount()
    let targuID = targ.getTypeId()
    if(targ.hasItemById(ITEM_REACTIVE_ARMOR))
        reactiveArmor1(targ, src, dam)
    if(targ.hasItemById(ITEM_SPIKE_OF_CRYPTLORD))
        spikeofCrypt(targ, src, dam)
    if(targ.hasItemById(ITEM_REACTIVE_ARMOR_II))
        reactiveArmor2(targ, src, dam)
    if(targ.hasItemById(ITEM_SPLIT_ARMOR))
        splitArmor(targ, src, dam)
    if(targ.hasItemById(ITEM_PERFECT_AQUAMARINE))
        perfectAquamrina(targ, src, dam)
    if(targ.hasItemById(ITEM_SPIKED_ARMOR))
        spikedArmor(targ, src, dam)
    if(targuID == WAVE_MOB_18)
        turtle(targ, src, dam)
    if(targuID == WAVE_BOSS_95)
        dragonTurtle(targ, src, dam)
    if(targ.getAbilityLevel('A0RI') > 0)
        pitLordThorn(targ, src)
    if(targ.getAbilityLevel('a0RL') > 0)
        pitLordDemonSkin(targ, src, dam)
    if(targ.hasAbility('BEah'))
        thornsAuraWave(targ, src, dam)