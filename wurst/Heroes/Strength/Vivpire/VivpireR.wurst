package VivpireR
import ClosureEvents
import Global_Variables
import MyUnitIds
import MyAbilityIds
import HashMap
import OrderIds
import ClosureForGroups
import DummyCaster
import ClosureTimers

var blood = 0
let strGain = [2,2,2,3,3,3,4,4,5] 
let preyUnitTimer = new HashMap<unit, CallbackSingle>

public function bloodGain()
    EventListener.add(EVENT_PLAYER_CHAT_FILTER) -> 
        if EventData.getChatMessage().substring(0, 6).toLowerCase() == "-blood"
            let p = GetTriggerPlayer()
            if (playerHero.get(p).getTypeId() == PLAY_HERO_3)
                printTimedToPlayer("You currently have |cffcf0505" + (blood.toString()) + " blood|r", 5, p)

    EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
        let kDie = GetKillingUnit()
        if(kDie.getTypeId() == PLAY_HERO_3)
            let uDie = GetDyingUnit()
            let curTime = preyUnitTimer.get(uDie)
            if(curTime != null)
                printTimedToPlayer("|cffcf0505Delicious Prey|r", 5, GetKillingUnit().getOwner())
                blood++
                kDie.addStr(strGain[kDie.getAbilityLevel(VIVPIRE_ABIL_4)-1])
                preyUnitTimer.remove(uDie)
                destroy curTime
    
    EventListener.onTargetCast(VIVPIRE_ABIL_4) (unit caster, unit target) ->
        if(preyUnitTimer.has(target) == false)
            let newTime = doAfter(10) -> 
                removebloodGain(target)
            activateBloodGain(caster)
            preyUnitTimer.put(target, newTime)
                
        else
            let curTime = preyUnitTimer.get(target)
            destroy curTime
            let newTime = doAfter(10) ->
                removebloodGain(target)
            activateBloodGain(caster)
            preyUnitTimer.put(target, newTime) 
                

function activateBloodGain(unit caster)
    let p = caster.getOwner()
    let pos = caster.getPos()
    forUnitsInRange(pos, 400) (unit u) ->
        if u.isEnemyOf(p)
            new DummyCaster()
            ..owner(p)
            ..origin(pos)
            ..castTarget(VIVPIRE_DUMMYABIL_3, 1, OrderIds.curse, u)


function removebloodGain(unit u)
    destroy preyUnitTimer.getAndRemove(u)

public function getBlood() returns int
    return blood

init
    bloodGain()