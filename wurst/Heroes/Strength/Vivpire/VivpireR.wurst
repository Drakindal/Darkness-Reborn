package VivpireR
import ClosureEvents
import Global_Variables
import MyUnitIds
import MyAbilityIds
import HashMap
import OrderIds
import DummyCaster
import HashSet
import ClosureTimers

var blood = 0
let strGain = [1,1,1,2,2,2,3,3,3]
let preySet = new HashSet<unit>

public function bloodGain()
    EventListener.add(EVENT_PLAYER_CHAT_FILTER) -> 
        if EventData.getChatMessage().substring(0, 6).toLowerCase() == "-blood"
            let p = EventData.getTriggerPlayer()
            if (playerHero.get(p).getTypeId() == PLAY_HERO_3)
                printTimedToPlayer("You currently have |cffcf0505" + (blood.toString()) + " blood|r", 5, p)

    EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
        let kDie = EventData.getKillingUnit()
        if(kDie.getTypeId() == PLAY_HERO_3)
            let uDie = EventData.getDyingUnit()
            if(preySet.has(uDie))
                printTimedToPlayer("|cffcf0505Delicious Prey|r", 5, GetKillingUnit().getOwner())
                blood++
                kDie.addStr(strGain[kDie.getAbilityLevel(VIVPIRE_ABIL_4)-1])
                uDie.removePrey()
    
    EventListener.onTargetCast(VIVPIRE_ABIL_4) (unit caster, unit target) ->
        activateBloodGain(caster, target)


function unit.preyTarget(vec2 pos, player p)
    preySet.add(this)
    new DummyCaster()
        ..owner(p)
        ..origin(pos)
        ..castTarget(VIVPIRE_DUMMYABIL_3, 1, OrderIds.curse, this)
    doAfter(10) -> 
        this.removePrey()

function activateBloodGain(unit caster, unit target)
    let p = caster.getOwner()
    let pos = caster.getPos()
    let lvl = caster.getAbilityLevel(VIVPIRE_ABIL_4)

    preySet.add(target)
        
    let tempGroup = CreateGroup()
    tempGroup.enumUnitsInRange(pos, 400)
    tempGroup.remove(target)
    for int i = 1 to 2*lvl
        let u = tempGroup.getRandom()
        u.preyTarget(pos, p)
        tempGroup.remove(u)
    tempGroup.clear()
    tempGroup.destr()

function unit.removePrey()
    preySet.remove(this)

public function getBlood() returns int
    return blood

init
    bloodGain()