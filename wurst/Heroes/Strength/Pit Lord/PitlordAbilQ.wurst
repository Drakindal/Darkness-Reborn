package PitlordAbilQ

import ClosureTimers
import ClosureForGroups
import SoundUtils
import Abilities
import DummyCaster
import OrderIds
import ClosureEvents
import MyAbilityIds
import codeDamageTypings
constant METEOR_SOUND = new SoundDefinition(Sounds.catapultMissile4, false, true)

init
    rainofFire()

function rainofFire()
    EventListener.onPointCast(PITLORD_ABIL_1) (unit caster, vec2 target) ->
        doAfter(1) -> 
            let lvl = caster.getAbilityLevel(PITLORD_ABIL_1)
            let dmg = lvl * 3 * caster.getStr(true).toReal()
            METEOR_SOUND.playOnPoint(target.toVec3())
            flashEffect(Abilities.fireLordDeathExplode, target, 2)
            forUnitsInRange(target, 175. + 25 * lvl, true) (unit u) ->
                if caster.isEnemyOf(u)
                    new DummyCaster()
                    ..owner(GetOwningPlayer(caster))
                    ..origin(vec2(caster.getX(), caster.getY()))
                    ..delay(1)
                    ..castTarget(ABILITY_DUMMY_21, lvl, OrderIds.cripple, u)
                    caster.magicalDamageCode(dmg, u)
                    doPeriodicallyCounted(1, lvl) (CallbackCounted cb) ->
                        if u.isAlive()
                            caster.magicalDamageCode(caster.getStr(true).toReal(), u)