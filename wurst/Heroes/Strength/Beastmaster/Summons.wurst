package Summons
import MyUnitIds
import StandardTextTags
import ClosureEvents
import MyAbilityIds
public int spirits = 0
texttag spiritText
init
    quilBeast()

function summonDeath(unit beastmaster, unit summon)
    EventListener.add(summon, EVENT_PLAYER_UNIT_DEATH) -> 
        let lvl = beastmaster.getAbilityLevel(ABILITY_7)
        beastmaster.addStr(lvl)
        spirits = spirits + 1
        if spirits % 5 == 0
            spiritText = createLumberBountyTextTag(beastmaster, spirits)
            spiritText.setColor(colorA(93, 247, 55, 255))
            spiritText.setText("Spirits: "+I2S(spirits))

function quilBeastCond() returns boolean
    return GetUnitTypeId(GetSummoningUnit()) == PLAY_HERO_5
function quilBeast()
    CreateTrigger()..registerAnyUnitEvent(EVENT_PLAYER_UNIT_SUMMON)..addCondition(Condition(function quilBeastCond))..addAction() ->
        let summon = GetSummonedUnit()
        let summoner = GetSummoningUnit()
        let str = summoner.getStr(true)
        
        if summon.getName() == "Misha Bear"
            summon.setMaxHP(((0.5*str+summon.getMaxHP()*0.04)*25).toInt(), true)
            summon.setArmor(0.5 * summoner.getArmor())
            summon.setBaseDamage((0.25 * str).toInt(), 0)
        
        if summon.getName() == "Quilbeast"
            summon.setMaxHP(((0.25*str+summon.getMaxHP()*0.04)*25).toInt(), true)
            summon.setArmor(0.25 * summoner.getArmor())
            summon.setBaseDamage((0.5 * str).toInt(), 0)
        
        if summon.getName() == "Hawk"
            summon.setMaxHP(((0.1*str+summon.getMaxHP()*0.04)*25).toInt(), true)
            summon.setArmor(0.1 * summoner.getArmor())
            summon.setBaseDamage(1 * str, 0)

        summonDeath(summoner, summon)