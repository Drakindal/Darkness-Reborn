package A00P_W
import MyUnitIds
import OrderIds
import MyAbilityIds
import ClosureEvents
import codeDamageTypings
import LinkedList
let arrowToggle = new LinkedList<unit>

init
    darkArrowToggle()

public function unit.getDarkArrowToggle() returns bool
    return arrowToggle.has(this)

function darkArrowToggle()
    EventListener.add(EVENT_PLAYER_UNIT_ISSUED_ORDER) -> 
        let u = GetTriggerUnit()
        let uid = u.getTypeId()
        if(uid == PLAY_HERO_16 or uid == WAVE_HERO_16)
            if(GetIssuedOrderId() == OrderIds.flamingarrows)
                arrowToggle.add(u)
            else if GetIssuedOrderId() == OrderIds.flamingarrows + 1
                arrowToggle.remove(u)

public function darkArrow(unit caster, unit target)
    let lvl = GetUnitAbilityLevel(caster, DARKRANGER_ABIL_2)
    let dmg = (caster.getAgi(true)*lvl*0.5)
    if(caster.getMana() < 5 * lvl)
        caster.issueImmediateOrderById(OrderIds.flamingarrows + 1)
    caster.magicalDamageCode(dmg, target)
    caster.addHP(dmg * 0.5* (1-target.getMagicResist()))