package TinkerArea
import MyUnitIds
import ClosureTimers
import Global_Variables
unit array tidecaller = []
init
    tinkerAreaEnterLeave() // From Base to Tinker's Area & Vice versa
    banTinkerArea() // Checks if entering unit is someone who should be allowed to visit Tinker's area or not
    tideCallerLeave() // Checks if the tidecallers tries to leave their spawn-point
    tinkerTidecallers() // Spawns 15 tidecallers for tinker to farm

function tinkerAreaEnterLeave()
    CreateTrigger()..registerRectEnterEventSource(gg_rct_EnterTinkerArea)..addAction() -> 
        let enteringUnit = GetEnteringUnit()
        let unitId = playerHero.get(enteringUnit.getOwner()).getTypeId()
        if unitId == HERO_TINKER
            enteringUnit.setPos(gg_rct_LeaveTinkerArea.getCenterX(), gg_rct_LeaveTinkerArea.getCenterY()-200)
        else
            printTimedToPlayer("Only Builders can enter here", 5, enteringUnit.getOwner())
            enteringUnit.setPos(gg_rct_EnterTinkerArea.getCenterX(), gg_rct_EnterTinkerArea.getCenterY()+200)
    CreateTrigger()..registerRectEnterEventSource(gg_rct_LeaveTinkerArea)..addAction() ->
        let enteringUnit = GetEnteringUnit()
        let unitId = playerHero.get(enteringUnit.getOwner()).getTypeId()
        if unitId == HERO_TINKER
            enteringUnit.setPos(gg_rct_EnterTinkerArea.getCenterX(), gg_rct_EnterTinkerArea.getCenterY()+200)
        else
            printTimedToPlayer("Only Builders can enter here", 5, enteringUnit.getOwner())
            enteringUnit.setPos(gg_rct_LeaveTinkerArea.getCenterX(), gg_rct_LeaveTinkerArea.getCenterY()-200)

function banTinkerArea()
    CreateTrigger()..registerRectEnterEventSource(gg_rct_TinkerArea)..addAction() ->
        let enteringUnit = GetEnteringUnit()
        let owner = enteringUnit.getOwner()
        if playerHero.get(owner).getTypeId() == HERO_TINKER or owner.getId() == PLAYER_NEUTRAL_AGGRESSIVE
            DoNothing()
        else
            printTimedToPlayer("Only Builders can enter here", 5, enteringUnit.getOwner())
            enteringUnit.setPos(gg_rct_Base.getCenter())

function tideCallerLeave() 
    CreateTrigger()..registerRectEnterEventSource(gg_rct_TidecallerEntrance)..addAction() ->
        if GetEnteringUnit().getTypeId() == TINKERTIDECALLER
            GetEnteringUnit().setPos(gg_rct_TidecallersSpawn.randomPoint())

function tidecallerRespawn(unit u)
    CreateTrigger()..registerUnitEvent(u, EVENT_UNIT_DEATH)..addAction() ->
        let facingUp = 90.00
        let u1 = GetDyingUnit()
        doAfter(5) -> 
            for i = 1 to 15
                if u1 == tidecaller[i]
                    tidecaller[i] = createUnit(Player(PLAYER_NEUTRAL_AGGRESSIVE), TINKERTIDECALLER, gg_rct_TidecallersSpawn.randomPoint(), facingUp.asAngleDegrees())
                    tidecallerRespawn(tidecaller[i])
                    
function tinkerTidecallers() 
    let facingUp = 90.00
    for i = 1 to 15
        tidecaller[i] = createUnit(Player(PLAYER_NEUTRAL_AGGRESSIVE), TINKERTIDECALLER, gg_rct_TidecallersSpawn.randomPoint(), facingUp.asAngleDegrees())
        tidecallerRespawn(tidecaller[i])